#include "front_window_exedir_getter.h"

#import <Cocoa/Cocoa.h>

#include <cstdio>   // FILE, popen, pclose, fgets
#include <stdexcept>
#include <string>

#include <qfile.h>
#include <qfileinfo.h>

#include "config.h"
#include "utils/file_io.h"

static std::string runCommand(const std::string& cmd)
{
    char buf[256] = {0};
    std::string out;

    FILE* pipe = popen(cmd.c_str(), "r");
    if (!pipe)
        throw std::runtime_error("Failed to open pipe");
    while (fgets(buf, sizeof(buf), pipe))
        out += buf;
    pclose(pipe);

    return out;
}

QString getFocusedFileManagerDir()
{
    static const std::string cmd =
        readAllFromFile(APP_RESOURCE_DIRPATH + "/scripts/get_focused_finder_dir.sh").toStdString();
    std::string out = runCommand(cmd);

    QString path = QString::fromStdString(out);
    // 去除首尾空白字符。
    path = path.trimmed();
    // 如果成功返回路径，其会被单引号包裹。
    if (path.size() < 2 || !path.startsWith('\'') || !path.endsWith('\''))
        throw std::runtime_error(std::string("Failed to get directory path from Finder: ") + out);
    // 去除首尾引号。
    path.remove(0, 1);
    path.chop(1);

    if (!QFileInfo(path).isAbsolute())
        throw std::runtime_error(std::string("Failed to get directory path from Finder: ") + out);

    return path;
}

QString getFrontWindowExeDir()
{
    try
    {
        // 尝试获取处于焦点状态的Finder所在目录路径。
        return getFocusedFileManagerDir();
    }
    catch (const std::exception& e)
    {
        // 如果获取Finder目录路径出现错误则回退至获取前景窗口可执行文件所在目录路径。

        @autoreleasepool
        {
            NSRunningApplication* app = [[NSWorkspace sharedWorkspace] frontmostApplication];
            if (!app)
                throw std::runtime_error("No frontmost application");

            NSURL* exeURL = [app executableURL];
            if (!exeURL)
                throw std::runtime_error("No executable URL");

            QString exePath = QString::fromUtf8([[exeURL path] UTF8String]);
            QFileInfo fi(exePath);
            return fi.absolutePath();
        }
    }
}
